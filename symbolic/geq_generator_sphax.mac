/*-----------------------------------------------------------------------------
Generator of the geodesic equations for a general axisymmetric stationary metric
in spherical-like coordinates. This serves as a basis for any metric that can
be expressed as ds^2 = -V dt + 2W dt dph + X dr^2 + Y dth^2 + Z dph^2
------------------------------------------------------------------------------*/

load("ctensor")$

/* Spherical-like coordinates*/
ct_coords:[t,r,th,ph]$

/* lg: covariant metric g_{mu,nu} */
lg: zeromatrix(4,4)$

depends([pv, pw, px, py, pz],[r,th])$
depends([At, Ar, Ath, Aph],[t,r,th,ph])$
lg[1,1]: -pv $
lg[1,4]: pw $
lg[4,1]: lg[1,4] $
lg[2,2]: px $
lg[3,3]: py $
lg[4,4]: pz $

/* calculate the inverse metric and mixed christoffel symbols */
cmetric()$
christof(false)$

pot_symb: [pv, pw, px, py, pz]$
empot_symb: [At, Ar, Ath, Aph]$
rth_symb: [t, r, th, ph]$

for i thru 4 do
    for j thru 4 do
        for k thru 4 do
            for l thru 2 do
              for h thru 5 do(
                  mcs[i,j,k]: subst(dpot[h,l],'diff(pot_symb[h],ct_coords[l+1]),mcs[i,j,k])
              )$

for i thru 4 do
  for j thru 4 do
    for k thru 4 do
      for h thru 5 do(
        mcs[i,j,k]: subst(pot[h],pot_symb[h],mcs[i,j,k])
      )$

for i thru 4 do
  for j thru 4 do
    for k thru 5 do(
      lg[i,j]: subst(pot[k],pot_symb[k],lg[i,j])
    )$

/* Electromagnetic field tensors */
Fem1: zeromatrix(4,4)$
Fem2: zeromatrix(4,4)$

for i thru 4 do
    for j thru 4 do(
        Fem1[i,j]: diff(empot_symb[j],rth_symb[i]) - diff(empot_symb[i],rth_symb[j])
    )$

for i thru 4 do
    for j thru 4 do
        for l thru 4 do
            for h thru 4 do(
                Fem1[i,j]: subst(dempot[h,l],'diff(empot_symb[h],ct_coords[l]),Fem1[i,j])
            )$

for i thru 4 do
    for j thru 4 do
        for h thru 4 do(  /* Cambiado de 2 a 4 */
            Fem1[i,j]: subst(empot[h],empot_symb[h],Fem1[i,j])
        )$

for k thru 4 do
    for i thru 4 do
        for j thru 4 do(
            Fem2[k,i]: Fem2[k,i] + ug[k,j]*Fem1[j,i]  /* Verifica el orden de Ã­ndices */
        )$

/* right hand side of the geodesic equations */
geqrhs_symb: [0,0,0,0]$

u: [vel(0), vel(1), vel(2), vel(3)]$

for k thru 4 do
    for i thru 4 do
        for j thru 4 do(
            geqrhs_symb[k]: geqrhs_symb[k] - mcs[i,j,k]*u[i]*u[j] + e*Fem2[k,i]*u[i]
        )$

load("f90")$

/* Writing the general metric */
with_stdout( "sphax-metric.f90",
  for i thru 4 do for j thru 4 do ( f90(metric(i-1,j-1) = lg[i,j]) )
)$

/* Writing the geodesic equations */
with_stdout( "sphax-geq.f90",
  for i thru 4 do ( f90( geqrhs(i-1) = geqrhs_symb[i]), print("") )
)$

/* Writing the electromagnetic tensor Fem1 */
with_stdout( "sphax-fem.f90",
  for i thru 4 do
    for j thru 4 do (
      f90( fem(i-1,j-1) = Fem1[i,j] )
    )
)$
